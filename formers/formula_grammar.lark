// This file defines grammar for valid expressions of equations in latex
// author: @Hansimov

// Terms:
//  - eqn: equation
//  - env: environment
//  - expr: cmd + arg
//  - cmd: command
//  - delim: delimiter
//  - char: character
//  - sym: symbol
//  - op: operator
//  - lb: left brace (\beingroup)
//  - rb: right brace (\endgroup)

latex_eqn: ([DISPLAY_STYLE] eqn)+
    eqn: eqn_with_env | eqn_with_scope | eqn_with_cmd | eqn_without_cmd
        eqn_with_env: begin_env_expr multiline_eqn end_env_expr
            begin_env_expr: BEGIN LB env RB col_align_expr
                col_align_expr: LB COL_ALIGN_SYM* RB
            end_env_expr: END LB env RB
            env: AMS_EQUATION_ENV | LOW_LEVEL_AMS_EQUATION_ENV | AMS_MATRIX_ENV
            multiline_eqn: eqn_in_env* (LINE_BREAK eqn_in_env)*
                eqn_in_env: eqn | ALIGN_SYM
        eqn_with_scope: LB eqn* RB
        eqn_with_cmd: eqn_with_scoped_cmd | eqn_with_delim_cmd | eqn_with_non_scoped_cmd
            eqn_with_scoped_cmd: LB scoped_cmd eqn* RB
                scoped_cmd: FONT_SIZE_CMD | BOLD_MATH_FONT_CMD | color_expr
                    color_expr: COLOR_CMD LB COLOR RB
            eqn_with_delim_cmd: (left_delim_expr eqn* right_delim_expr) | non_paired_delim_expr
                    left_delim_expr: LEFT_DELIM_CMD (LEFT_DELIM_SYM | NON_PAIRED_DELIM_SYM)
                    right_delim_expr: RIGHT_DELIM_CMD (RIGHT_DELIM_SYM | NON_PAIRED_DELIM_SYM)
                    non_paired_delim_expr: NON_PAIRED_DELIM_CMD (LEFT_DELIM_SYM | RIGHT_DELIM_SYM | NON_PAIRED_DELIM_SYM)
            eqn_with_non_scoped_cmd: one_arg_expr | two_arg_expr
                one_arg_expr: one_arg_cmd eqn
                    one_arg_cmd: MATH_FONT_STYLE_CMD | SHORT_MATH_FONT_STYLE_CMD | TEXT_FONT_STYLE_CMD | SUB_SUP_CMD | ACCENT_CMD | EXTENSIBLE_ACCENT_CMD | OPERATOR_NAME_CMD | MOD_CMD | AFFIX_CMD | BOXED_CMD
                two_arg_expr: two_arg_cmd eqn eqn
                    two_arg_cmd: FRAC_CMD
        eqn_without_cmd: operator | symbol | char | control
            char: normal_char | lang_char
                normal_char: LOWER_ASCII_CHAR | UPPER_ASCII_CHAR | DIGIT_CHAR
                lang_char: GREEK_CHAR | AMS_GREEK_CHAR | AMS_ITALIC_GREEK_CHAR | AMS_HEBREW_CHAR
            operator: BINARY_OP | AMS_BINARY_OP | VARIABLE_SIZE_OP | AMS_VARIABLE_SIZE_OP | BINARY_RELATION_OP | AMS_BINARY_RELATION_OP | AMS_NEG_BINARY_RELATION_OP | SET_RELATION_OP | AMS_SET_RELATION_OP | INEQUALITY_OP | AMS_INEQUALITY_OP | AMS_TRIAG_RELATION_OP | ARROW_OP | AMS_ARROW_OP | AMS_NEG_ARROW_OP | HARPOON_OP | AMS_HARPOON_OP | LOG_LIKE_OP | AMS_LOG_LIKE_OP | LETTER_LIKE_OP | AMS_LETTER_LIKE_OP | AMS_ANGLE_OP
            symbol: LEFT_DELIM_SYM | RIGHT_DELIM_SYM | NON_PAIRED_DELIM_SYM | ESCAPED_SYM | PUNCT_SYM | MATH_TEXT_SYM | AMS_DELIM_SYM | DOT_SYM | AMS_DOT_SYM | MISC_MATH_SYM | MUSIC_SYM | CARD_SYM | SPACING_SYM | SKIP_SYM
            control: NO_TAG_CMD | LINE_BREAK

%import common.WS
%ignore WS

// ============ Commands ============ //

// -------- scoped cmds -------- //

// LINK: constants/macros.py#display-style-macros
DISPLAY_STYLE: "\displaystyle"

LB: "{"
RB: "}"
WB: " "

BEGIN: "\begin"
END: "\end"

// LINK: constants/macros.py#font-size-macros
FONT_SIZE_CMD: "\tiny" | "\scriptsize" | "\footnotesize" | "\small" | "\normalsize" | "\large" | "\Large" | "\LARGE" | "\huge" | "\Huge" | "\scriptstyle" | "\scriptscriptstyle"

COLOR_CMD: "\color"
COLOR: "black" | "white" | "red" | "green" | "blue" | "cyan" | "magenta" | "yellow" | "gray" | "grey" | "darkgray" | "lightgray" | "darkgrey" | "lightgrey" | "brown" | "lime" | "olive" | "orange" | "pink" | "purple" | "teal" | "violet"

// LINK: constants/macros.py#bold-math-font-macros
BOLD_MATH_FONT_CMD: "\boldmath"

// -------- environments -------- //

// LINK: constants/environments.py#ams-equation-envs
AMS_EQUATION_ENV: "equation" | "equation* " | "subequations " | "align" | "align*" | "gather" | "gather*" | "alignat" | "alignat* " | "multiline " | "multiline* " | "flalign" | "flalign*" | "cases" | "array" | "eqnarray" | "eqnarray*" | "subarray"

// LINK: constants/environments.py#low-level-ams-equation-envs
LOW_LEVEL_AMS_EQUATION_ENV: "gathered" | "aligned" | "alignedat" | "split"

// LINK: constants/environments.py#ams-matrix-envs
AMS_MATRIX_ENV: "matrix" | "pmatrix" | "pmatrix*" | "bmatrix" | "bmatrix*" | "Bmatrix" | "Bmatrix*" | "vmatrix" | "vmatrix*" | "Vmatrix" | "Vmatrix*" | "smallmatrix"

// -------- delimiter cmds and symbols -------- //

// LINK: constants/macros.py#ams-delimiter-macros
LEFT_DELIM_CMD: "\left"
RIGHT_DELIM_CMD: "\right"
// LINK: constants/macros.py#vertical-resize-macros
NON_PAIRED_DELIM_CMD: "\big" | "\Big" | "\bigg" | "\Bigg" | "\bigl" | "\Bigl" | "\biggl" | "\Biggl" | "\bigr" | "\Bigr" | "\biggr" | "\Biggr"

// LINK: constants/macros.py#variable-sized-delimiter-symbols
// LINK: constants/macros.py#ams-variable-sized-delimiter-symbols
// LINK: constants/macros.py#large-variable-sized-delimiter-symbols
// LINK: constants/macros.py#null-delimiter-symbols
LEFT_DELIM_SYM: "(" | "[" | "\{" |  "\lbrace" | "\lvert" | "\lVert" | "\langle" | "\lgroup" | "\lmoustache" | "\lceil" | "\lfloor"
RIGHT_DELIM_SYM: ")" | "]" | "\}" |  "\rbrace" | "\rvert" | "\rVert" | "\rangle" | "\rgroup" | "\rmoustache" | "\rceil" | "\rfloor"
NON_PAIRED_DELIM_SYM:  "|" | "\vert" | "|" | "\Vert"| "\|" | "\downarrow" | "\Downarrow" | "\uparrow" | "\Uparrow" | "\updownarrow" | "\Updownarrow" | "\arrowvert" | "\Arrowvert" | "\backslash" | "\slash" | "/" | "." 

// -------- non scoped cmds -------- //

// LINK: constants/macros.py#ams-math-font-macros
// LINK: constants/macros.py#math-alphabet-macros
// LINK: constants/macros.py#bold-math-font-macros
MATH_FONT_STYLE_CMD: "\mathbf" | "\mathrm" | "\mathcal" | "\mathsf" | "\mathtt" | "\mathit" | "\mathfrak" | "\mathbb" | "\mathscr" | "\mathnormal" | "\boldsymbol" | "\pmb"

// LINK: constants/macros.py#short-ams-math-font-macros
SHORT_MATH_FONT_STYLE_CMD: "\bf" | "\rm" | "\cal" | "\sf" | "\tt" | "\it" | "\bm" | "\it" | "\frak" | "\mit"

// LINK: constants/macros.py#ams-text-font-macros
TEXT_FONT_STYLE_CMD: "\text" | "\textrm" | "\textbf" | "\textit" | "\texttt" | "\textsf" | "\textup" | "\textnormal"

// LINK: constants/macros.py#sub-sup-macros
SUB_SUP_CMD: "^" | "_"

// LINK: constants/macros.py#math-mode-accent-macros
// LINK: constants/macros.py#ams-math-mode-accent-macros
ACCENT_CMD: "\acute" | "\bar" | "\breve" | "\check" | "\dot" | "\ddot" | "\grave" | "\hat" | "\mathring" | "\tilde" | "\vec" | "\dddot" | "\dddot"

// LINK: constants/macros.py#extensible-accent-macros
// LINK: constants/macros.py#overrightarrow-extensible-accent-macros
// LINK: constants/macros.py#ams-extensible-accent-macros
// LINK: constants/macros.py#ams-extensible-arrow-macros
EXTENSIBLE_ACCENT_CMD: "\overbrace" | "\overline" | "\overleftarrow" | "\overrightarrow" | "\sqrt" | "\underbrace" | "\underline" | "\widehat" | "\widetilde" | "\Overrightarrow" | "\overleftrightarrow" | "\underleftarrow" | "\underleftrightarrow" | "\underrightarrow" | "\xleftarrow" | "\xrightarrow"

// LINK: constants/macros.py#ams-operator-name-macros
OPERATOR_NAME_CMD: "\operatorname" | "\operatorname*" | "\mathop"

// LINK: constants/macros.py#ams-mod-macros
MOD_CMD: "\mod" | "\bmod" | "\pmod" | "\pod"

// LINK: constants/macros.py#ams-affix-macros
AFFIX_CMD: "\stackrel" | "\overset" | "underset" | "\sideset" | "\limits" | "\nolimits"

// LINK: constants/macros.py#ams-boxed-macros
BOXED_CMD: "\boxed"

// LINK: constants/macros.py#ams-frac-macros
// require two args
FRAC_CMD: "\frac" | "\dfrac" | "\tfrac" | "\cfrac" | "\binom" | "\dbinom" | "\tbinom"

// -------- special commands -------- //

// LINK: constants/macros.py#no-tag-macros
NO_TAG_CMD: "\notag" | "\nonumber"

// LINK: constants/macros.py#ams-substack-macros
SUBSTACK_CMD: "\substack"

// LINK: constants/macros.py#ams-matrix-dot-macros
MATRIX_DOT_CMD: "\hdotsfor"


// ========= Characters, Operators, Symbols ========= //

// -------- characters -------- //

// LINK: constants/symbols.py#ascii-symbols
LOWER_ASCII_CHAR: "a".."z"
UPPER_ASCII_CHAR: "A".."Z"
DIGIT_CHAR: "0".."9"

// LINK: constants/symbols.py#greek-symbols
GREEK_CHAR: "\alpha" | "\beta" | "\gamma" | "\delta" | "\epsilon" | "\varepsilon" | "\zeta" | "\eta" | "\theta" | "\vartheta" | "\iota" | "\kappa" | "\lambda" | "\mu" | "\nu" | "\xi" | "\omicron" | "\pi" | "\varpi" | "\rho" | "\varrho" | "\sigma" | "\varsigma" | "\tau" | "\upsilon" | "\phi" | "\varphi" | "\chi" | "\psi" | "\omega" | "\Gamma" | "\Delta" | "\Theta" | "\Lambda" | "\Xi" | "\Pi" | "\Sigma" | "\Upsilon" | "\Phi" | "\Psi" | "\Omega"
// LINK: constants/symbols.py#ams-greek-symbols
AMS_GREEK_CHAR: "\digamma" | "\varkappa"
// LINK: constants/symbols.py#ams-italic-greek-symbols
AMS_ITALIC_GREEK_CHAR: "\varGamma" | "\varDelta" | "\varTheta" | "\varLambda" | "\varXi" | "\varPi" | "\varSigma" | "\varUpsilon" | "\varPhi" | "\varPsi" | "\varOmega"
// LINK: constants/symbols.py#ams-hebrew-symbols
AMS_HEBREW_CHAR: "\beth" | "\gimel" | "\daleth"

// -------- operators -------- //

// LINK: constants/symbols.py#binary-operator-symbols
BINARY_OP: "\amalg" | "\ast" | "\bigcirc" | "\bigtriangledown" | "\bigtriangleup" | "\bullet" | "\cap" | "\cdot" | "\circ" | "\cup" | "\dagger" | "\ddagger" | "\diamond" | "\div" | "\lhd" | "\mp" | "\odot" | "\ominus" | "\oplus" | "\oslash" | "\otimes" | "\pm" | "\rhd" | "\setminus" | "\sqcap" | "\sqcup" | "\star" | "\times" | "\triangleleft" | "\triangleright" | "\unlhd" | "\unrhd" | "\uplus" | "\vee" | "\wedge" | "\wr"
// LINK: constants/symbols.py#ams-binary-operator-symbols
AMS_BINARY_OP: "\barwedge" | "\boxdot" | "\boxminus" | "\boxplus" | "\boxtimes" | "\Cap" | "\centerdot" | "\circledast" | "\circledcirc" | "\circleddash" | "\Cup" | "\curlyvee" | "\curlywedge" | "\divideontimes" | "\dotplus" | "\doublebarwedge" | "\intercal" | "\leftthreetimes" | "\ltimes" | "\rightthreetimes" | "\rtimes" | "\smallsetminus" | "\veebar"

// LINK: constants/symbols.py#variable-sized-math-operator-symbols
VARIABLE_SIZE_OP: "\bigcap" | "\bigcup" | "\bigodot" | "\bigoplus" | "\bigotimes" | "\bigsqcup" | "\biguplus" | "\bigvee" | "\bigwedge" | "\coprod" | "\int" | "\oint" | "\prod" | "\sum"
// LINK: constants/symbols.py#ams-variable-sized-math-operator-symbols
AMS_VARIABLE_SIZE_OP: "\iint" | "\iiint" | "\iiiint" | "\idotsint"

// LINK: constants/symbols.py#binary-relation-symbols
BINARY_RELATION_OP: "\approx" | "\asymp" | "\bowtie" | "\cong" | "\dashv" | "\doteq" | "\equiv" | "\frown" | "\Join" | "\mid" | "\models" | "\parallel" | "\perp" | "\prec" | "\preceq" | "\propto" | "\sim" | "\simeq" | "\smile" | "\succ" | "\succeq" | "\vdash"
// LINK: constants/symbols.py#ams-binary-relation-symbols
AMS_BINARY_RELATION_OP: "\approxeq" | "\backepsilon" | "\backsim" | "\backsimeq" | "\because" | "\between" | "\Bumpeq" | "\bumpeq" | "\circeq" | "\curlyeqprec" | "\curlyeqsucc" | "\doteqdot" | "\eqcirc" | "\fallingdotseq" | "\multimap" | "\pitchfork" | "\precapprox" | "\preccurlyeq" | "\precsim" | "\risingdotseq" | "\shortmid" | "\shortparallel" | "\smallfrown" | "\smallsmile" | "\succapprox" | "\succcurlyeq" | "\succsim" | "\therefore" | "\thickapprox" | "\thicksim" | "\varpropto" | "\Vdash" | "\vDash" | "\Vvdash"
// LINK: constants/symbols.py#ams-negated-binary-relation-symbols
AMS_NEG_BINARY_RELATION_OP: "\ncong" | "\nmid" | "\nparallel" | "\nprec" | "\npreceq" | "\shortmid" | "\shortparallel" | "\nsim" | "\nsucc" | "\nsucceq" | "\nvDash" | "\nvdash" | "\nVDash" | "\precnapprox" | "\precnsim" | "\succnapprox" | "\succnsim"

// LINK: constants/symbols.py#subset-superset-relation-symbols
SET_RELATION_OP: "\subset" | "\subseteq" | "\supset" | "\supseteq" | "\sqsubset" | "\sqsubseteq" | "\sqsupset" | "\sqsupseteq"
// LINK: constants/symbols.py#ams-subset-superset-relation-symbols
AMS_SET_RELATION_OP: "\nsubseteq" | "\nsupseteq" | "\nsupseteqq" | "\sqsubset" | "\sqsupset" | "\Subset" | "\subseteqq" | "\subsetneq" | "\subsetneqq" | "\Supset" | "\supseteqq" | "\supsetneq" | "\supsetneqq" | "\varsubsetneq" | "\varsubsetneqq" | "\varsupsetneq" | "\varsupsetneqq"

// LINK: constants/symbols.py#inequality-symbols
INEQUALITY_OP: "\le" | "\ge" | "\geq" | "\gg" | "\leq" | "\ll" | "\neq"
// LINK: constants/symbols.py#ams-inequality-symbols
AMS_INEQUALITY_OP: "\eqslantgtr" | "\eqslantless" | "\geqq" | "\geqslant" | "\ggg" | "\gnapprox" | "\gneq" | "\gneqq" | "\gnsim" | "\gtrapprox" | "\gtrdot" | "\gtreqless" | "\gtreqqless" | "\gtrless" | "\gtrsim" | "\gvertneqq" | "\leqq" | "\leqslant" | "\lessapprox" | "\lessdot" | "\lesseqgtr" | "\lesseqqgtr" | "\lessgtr" | "\lesssim" | "\lll" | "\lnapprox" | "\lneq" | "\lneqq" | "\lnsim" | "\lvertneqq" | "\ngeq" | "\ngeqq" | "\ngeqslant" | "\ngtr" | "\nleq" | "\nleqq" | "\nleqslant" | "\nless"

// LINK: constants/symbols.py#ams-triangle-relation-symbols
AMS_TRIAG_RELATION_OP: "\blacktriangleleft" | "\blacktriangleright" | "\ntriangleleft" | "\ntrianglelefteq" | "\ntriangleright" | "\ntrianglerighteq" | "\trianglelefteq" | "\triangleq" | "\trianglerighteq" | "\vartriangleleft" | "\vartriangleright"

// LINK: constants/symbols.py#arrow-symbols
ARROW_OP: "\Downarrow" | "\downarrow" | "\hookleftarrow" | "\hookrightarrow" | "\leadsto" | "\leftarrow" | "\Leftarrow" | "\leftrightarrow" | "\Leftrightarrow" | "\longleftarrow" | "\Longleftarrow" | "\longleftrightarrow" | "\Longleftrightarrow" | "\longmapsto" | "\longrightarrow" | "\Longrightarrow" | "\mapsto" | "\nearrow" | "\nwarrow" | "\rightarrow" | "\Rightarrow" | "\searrow" | "\swarrow" | "\uparrow" | "\Uparrow" | "\updownarrow" | "\Updownarrow" | "\to" | "\gets"
// LINK: constants/symbols.py#ams-arrow-symbols
AMS_ARROW_OP: "\circlearrowleft" | "\circlearrowright" | "\curvearrowleft" | "\curvearrowright" | "\dashleftarrow" | "\dashrightarrow" | "\downdownarrows" | "\leftarrowtail" | "\leftleftarrows" | "\leftrightarrows" | "\leftrightsquigarrow" | "\Lleftarrow" | "\looparrowleft" | "\looparrowright" | "\Lsh" | "\rightarrowtail" | "\rightleftarrows" | "\rightrightarrows" | "\rightsquigarrow" | "\Rsh" | "\twoheadleftarrow" | "\twoheadrightarrow" | "\upuparrows"
// LINK: constants/symbols.py#ams-negated-arrow-symbols
AMS_NEG_ARROW_OP: "\nleftarrow" | "\nLeftarrow" | "\nleftrightarrow" | "\nLeftrightarrow" | "\nrightarrow" | "\nRightarrow"

// LINK: constants/symbols.py#harpoon-symbols
HARPOON_OP: "\leftharpoondown" | "\leftharpoonup" | "\rightharpoondown" | "\rightharpoonup" | "\rightleftharpoons"
// LINK: constants/symbols.py#ams-harpoon-symbols
AMS_HARPOON_OP: "\downharpoonleft" | "\downharpoonright" | "\leftrightharpoons" | "\rightleftharpoons" | "\upharpoonleft" | "\upharpoonright"

// LINK: constants/symbols.py#log-like-symbols
LOG_LIKE_OP: "\arccos" | "\cos" | "\csc" | "\exp" | "\ker" | "\limsup" | "\min" | "\sinh" | "\arcsin" | "\cosh" | "\deg" | "\gcd" | "\lg" | "\ln" | "\Pr" | "\sup" | "\arctan" | "\cot" | "\det" | "\hom" | "\lim" | "\log" | "\sec" | "\tan" | "\arg" | "\coth" | "\dim" | "\inf" | "\liminf" | "\max" | "\sin" | "\tanh"
// LINK: constants/symbols.py#ams-log-like-symbols
AMS_LOG_LIKE_OP: "\injlim" | "\projlim" | "\varinjlim" | "\varliminf" | "\varlimsup" | "\varprojlim"

// LINK: constants/symbols.py#letter-like-symbols
LETTER_LIKE_OP: "\bot" | "\ell" | "\exists" | "\forall" | "\hbar" | "\Im" | "\imath" | "\in" | "\jmath" | "\ni" | "\partial" | "\Re" | "\top" | "\wp"
// LINK: constants/symbols.py#ams-like-symbols
AMS_LETTER_LIKE_OP: "\Bbbk" | "\circledR" | "\circledS" | "\complement" | "\Finv" | "\Game" | "\hbar" | "\hslash" | "\nexists"

// LINK: constants/symbols.py#ams-angle-symbols
AMS_ANGLE_OP: "\angle" | "\measuredangle" | "\sphericalangle"

// -------- symbols -------- //

// LINK: constants/symbols.py#escaped-symbols
ESCAPED_SYM: "\&" | "\%" | "\$" | "\#" | "\_" | "\{" | "\}" | "\~" | "\^" | "\\"
// LINK: constants/symbols.py#punctuation-symbols
// TODO: correctly parse double quote (\")
PUNCT_SYM: "." | "," | ":" | ";" | "!" | "?" | "'" | "`"| "-" | "\\" | "<" | ">" | "+" | "=" | "*" | "@"
LINE_BREAK: "\\\\"

// LINK: constants/symbols.py#align-symbols
ALIGN_SYM: "&"
COL_ALIGN_SYM: "c" | "l" | "r"

// LINK: constants/symbols.py#latex2e-math-text-symbols
// LINK: constants/symbols.py#ams-math-text-symbols
MATH_TEXT_SYM: "\copyright" | "\dag" | "\ddag" | "\dots" | "\P" | "\pounds" | "\S" | "\checkmark" | "\circledR" | "\maltese"

// LINK: constants/symbols.py#ams-delimiter-symbols
AMS_DELIM_SYM: "\ulcorner" | "\llcorner" | "\urcorner" | "\lrcorner"

// LINK: constants/symbols.py#dot-symbols
DOT_SYM: "\cdotp" | "\cdots" | "\colon" | "\ddots" | "\ldotp" | "\ldots" | "\vdots"
// LINK: constants/symbols.py#ams-dot-symbols
AMS_DOT_SYM: "\because" | "\dotsb" | "\dotsc" | "\dotsi" | "\dotsm" | "\dotso" | "\therefore"

// LINK: constants/symbols.py#latex2e-misc-math-symbols
// LINK: constants/symbols.py#ams-misc-math-symbols
MISC_MATH_SYM: "\aleph" | "\emptyset" | "\angle" | "\backslash" | "\Box" | "\Diamond" | "\infty" | "\mho" | "\nabla" | "\neg" | "\prime" | "\surd" | "\triangle" | "\backprime" | "\bigstar" | "\blacklozenge" | "\blacksquare" | "\blacktriangle" | "\blacktriangledown" | "\diagdown" | "\diagup" | "\eth" | "\lozenge" | "\mho" | "\square" | "\triangledown" | "\varnothing" | "\vartriangle"

// LINK: constants/symbols.py#latex2e-musical-symbols
MUSIC_SYM: "\flat" | "\natural" | "\sharp"
// LINK: constants/symbols.py#latex2e-playing-card-suit-symbols
CARD_SYM: "\clubsuit" | "\diamondsuit" | "\heartsuit" | "\spadesuit"

// LINK: constants/symbols.py#ams-spacing-symbols
SPACING_SYM: "\thinspace" | "\medspace" | "\thickspace" | "\quad" | "\qquad" | "\negthinspace" | "\negmedspace" | "\negthickspace" | "\:" | "\," | "\;" | "\!" | "~" | "\ "

// LINK: constants/symbols.py#skip-symbols
SKIP_SYM: "\bigskip" | "\medskip" | "\smallskip"
